apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

Properties properties = new Properties()
File propertiesFile = rootProject.file("secret.properties")
properties.load(new FileInputStream(propertiesFile))

android {
    buildToolsVersion '28.0.3'
    compileSdkVersion 27
    defaultConfig {
        applicationId "club.bweakfast.foodora"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 9
        versionName "1.4.2-alpha"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes.each {
        properties.each { line ->
            def key = line.getKey()
            def value = line.getValue()
            it.buildConfigField "String", key, value
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashReporting: "true"]
        }
    }
    flavorDimensions "flavour"
    productFlavors {
        mock {
            dimension "flavour"
        }
        real {
            dimension "flavour"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.core:core-ktx:0.3'
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:preference-v7:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.6'
    implementation 'com.facebook.fresco:fresco:1.9.0'
    implementation 'com.github.javiersantos:BottomDialogs:1.2.1'
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation 'com.google.firebase:firebase-core:16.0.5'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.1'
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'nl.littlerobots.rxlint:rxlint:1.5'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    mockImplementation 'com.github.salomonbrys.kotson:kotson:2.5.0'
}

androidExtensions {
    experimental = true
}

apply plugin: 'com.google.gms.google-services'
ext.enableCrashlytics = true
